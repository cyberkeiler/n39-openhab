import org.openhab.core.library.types.*
import org.joda.time.*
import java.util.Date
import java.text.SimpleDateFormat

rule "N39 opened"
when
	Item SpaceStatus changed to ON
then
	// Um Scharfe Kanten im Diagramm zu haben setze erstmal alte Werte erneut
	postUpdate(SpaceStatusChartClosed, 1)
	postUpdate(SpaceStatusChartOpen, 0)

	logInfo("SpaceStatus", "N39 SpaceStatus open!")

	// Die großen Rolläden hochfahren
	sendCommand(gRolladenFront,UP)

	postUpdate(Stat_MemberIn, 1)
	sendCommand(Grp_SpaceStandard, ON)	// Schalte Items in der Gruppe Grp_SpaceStandard ein

	// Tweete, dass Space (wieder) offen ist
	var SimpleDateFormat df = new SimpleDateFormat( "YYYY-MM-dd HH:mm" )
	var String Timestamp = df.format( new Date() )
	var DateTime dateTime = new DateTime((SpaceStatusChangeDate.state as DateTimeType).calendar.timeInMillis)
	if (dateTime.plusMinutes(15).isBefore(now)) {
		sendTweet('#Spacetime im @netz39! ('+Timestamp+')')
	}
	else{
		sendTweet('Es geht doch noch weiter! Immer noch #Spacetime im @netz39! ('+Timestamp+')')
	}

	// Setzte Werte für Status neu
	postUpdate(SpaceStatusChangeDate, new DateTimeType())
	postUpdate(SpaceStatusChartOpen, 1)
	postUpdate(SpaceStatusChartClosed, 0)
end

rule "N39 closed"
when
	Item SpaceStatus changed to OFF
then
	// Um Scharfe Kanten im Diagramm zu haben setze erstmal alte Werte erneut
	postUpdate(SpaceStatusChartClosed, 0)
	postUpdate(SpaceStatusChartOpen, 1)

	logInfo("SpaceStatus", "N39 SpaceStatus closed!")

	// Die großen Rolläden herunterfahren
	sendCommand(gRolladenFront,DOWN)

	postUpdate(Stat_MemberIn, 0)
	sendCommand(Grp_SpaceShutdown, OFF) // Schalte Items in der Gruppe Grp_SpaceShutdown aus

	// Tweete, dass Space geschlossen wurde
	var SimpleDateFormat df = new SimpleDateFormat( "YYYY-MM-dd HH:mm" )
	var String Timestamp = df.format( new Date() )
	sendTweet('Das @netz39 beendet die #Spacetime! ('+Timestamp+')')

	// Setzte Werte für Status neu
	postUpdate(SpaceStatusChangeDate, new DateTimeType())
	postUpdate(SpaceStatusChartOpen, 0)
	postUpdate(SpaceStatusChartClosed, 1)
end
